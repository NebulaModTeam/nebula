Shader "Nebula/BatchedEmoji"
{
    Properties
    {
        [PerRendererData] _MainTex ("Sprite Texture", 2D) = "white" {}
        _Color ("Tint", Color) = (1,1,1,1)

        _SpriteSheetTex ("Emoji sprite sheet", 2D) = "white" {}
        _Grid ("Grid", Vector) = (1,1,1,1)

        _StencilComp ("Stencil Comparison", Float) = 8
        _Stencil ("Stencil ID", Float) = 0
        _StencilOp ("Stencil Operation", Float) = 0
        _StencilWriteMask ("Stencil Write Mask", Float) = 255
        _StencilReadMask ("Stencil Read Mask", Float) = 255

        _ColorMask ("Color Mask", Float) = 15

        [Toggle(UNITY_UI_ALPHACLIP)] _UseUIAlphaClip ("Use Alpha Clip", Float) = 0
    }

    SubShader
    {
        Tags
        {
            "Queue"="Transparent"
            "IgnoreProjector"="True"
            "RenderType"="Transparent"
            "PreviewType"="Plane"
            "CanUseSpriteAtlas"="True"
        }

        Stencil
        {
            Ref [_Stencil]
            Comp [_StencilComp]
            Pass [_StencilOp]
            ReadMask [_StencilReadMask]
            WriteMask [_StencilWriteMask]
        }

        Cull Off
        Lighting Off
        ZWrite Off
        ZTest [unity_GUIZTestMode]
        Blend SrcAlpha OneMinusSrcAlpha
        ColorMask [_ColorMask]

        Pass
        {
        CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma target 5.0

            #include "UnityCG.cginc"
            #include "UnityUI.cginc"

            #pragma multi_compile_local _ UNITY_UI_CLIP_RECT
            #pragma multi_compile_local _ UNITY_UI_ALPHACLIP

            struct appdata_t
            {
                float4 vertex   : POSITION;
                float4 color    : COLOR;
                float2 texcoord : TEXCOORD0;
                UNITY_VERTEX_INPUT_INSTANCE_ID
            };

            struct v2f
            {
                float4 vertex   : SV_POSITION;
                fixed4 color    : COLOR;
                float2 texcoord  : TEXCOORD0;
                float4 worldPosition : TEXCOORD1;
                UNITY_VERTEX_OUTPUT_STEREO
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;
            
            sampler2D _SpriteSheetTex;
            float4 _SpriteSheetTex_ST;
            uniform float4 _SpriteSheetTex_TexelSize;

            fixed4 _Color;
            fixed4 _TextureSampleAdd;
            float4 _ClipRect;
            
            float4 _Grid; 

            StructuredBuffer<uint> _EmojiBuffer;

            v2f vert(appdata_t v)
            {
                v2f OUT;
                UNITY_SETUP_INSTANCE_ID(v);
                UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(OUT);
                OUT.worldPosition = v.vertex;
                OUT.vertex = UnityObjectToClipPos(OUT.worldPosition);

                OUT.texcoord = v.texcoord;

                OUT.color = v.color * _Color;
                return OUT; 
            }

            fixed4 frag(v2f IN) : SV_Target 
            {
                float2 size = 1 / _Grid;
 
                // Calculate emoji position
                fixed2 fixUV = IN.texcoord;
                fixUV.y = 1 - fixUV.y;
                int2 pos = fixUV / size;
                uint index = pos.x + pos.y * _Grid.x;

                // Get emoji data from buffer
                index = _EmojiBuffer[index]; 
                if (index == 0) discard;
                index = index - 1;

                // Calculate emoji position in sprite sheet
                pos = int2(index % 62, index / 62);
                pos.y = -pos.y - 1;

                // Calculate sheet UV position
                float2 spriteUV = frac(IN.texcoord * _Grid) * 1.1f;
                spriteUV = clamp(spriteUV, 0, 1);
                if (spriteUV.x < 0.01f || spriteUV.x > 0.99f) discard;
                if (spriteUV.y < 0.01f || spriteUV.y > 0.99f) discard;

                float2 texPos = (pos + spriteUV) * 66 * _SpriteSheetTex_TexelSize.xy;
                
                // Sample texture and output color
                half4 emojiColor = tex2D(_SpriteSheetTex, texPos);
                half4 color = (emojiColor + _TextureSampleAdd) * IN.color;

                #ifdef UNITY_UI_CLIP_RECT
                color.a *= UnityGet2DClipping(IN.worldPosition.xy, _ClipRect);
                #endif

                #ifdef UNITY_UI_ALPHACLIP
                clip (color.a - 0.001);
                #endif

                return color;
            }
        ENDCG
        }
    }
}